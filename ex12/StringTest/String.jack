// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents a String object. Implements the String type.
 */
class String {
	
	field Array str;
	field int length;
	field int max;
	
    /** Constructs a new empty String with a maximum length of maxLength. */
    constructor String new(int maxLength) {
		if( maxLength = 0 ) {
            let maxLength = 1;
        }
		let str = Array.new(maxLength);
		let length = 0;
		let max = maxLength;
		return this;
    }

    /** De-allocates the string and frees its space. */
    method void dispose() {
		do Array.dispose(str);
		return;
    }

    /** Returns the current length of this String. */
    method int length() {
		return length;
    }

    /** Returns the character at location j. */
    method char charAt(int j) {
		return str[j];
    }

    /** Sets the j'th character of this string to be c. */
    method void setCharAt(int j, char c) {
		let str[j] = c;
		return;
    }

    /** Appends the character c to the end of this String.
     *  Returns this string as the return value. */
    method String appendChar(char c) {
		if (length < max) {
			let str[length] = c;
			let length = length + 1;
		}
		return this;
    }

    /** Erases the last character from this String. */
    method void eraseLastChar() {
		if (length > 0) {
			let length = length - 1;
		}
		return;
    }

    /** Returns the integer value of this String until the first non
     *  numeric character. */
    method int intValue() {
		var int i;
		var int sum;
		var boolean neg;
		
		let sum = 0;
		if ((length > 0) & (str[0] = 45)) { //'-' sign in ascii
			let neg = true;
			let i = 1;
		}
		else {
			let neg = false;
			let i = 0;
		}
		
		while (i < length) {
			if (str[i] > 47 & str[i] < 58) { //the char is a digit between 0 to 9 in ascii
				let sum = (sum * 10) + (str[i] - 48);
			}
			let i = i + 1;
		}
		if (neg) {
			return -sum;
		}
		else {
			return sum;
		}
    }

    /** Sets this String to hold a representation of the given number. */
    method void setInt(int number) {
		let length = 0;
		if (number < 0) {
			let number = -number;
			do appendChar(45); //add leading '-'
		}
		do recSetInt(number);
		return;
    }
    
    method void recSetInt(int number) {
		var int mod;
		var int div;

		let div = number / 10;
		let mod = number - (div * 10);
		
		if (number < 10)
		{
			do appendChar(mod + 48);
		}
		else{
			do recSetInt(div);
			do appendChar(mod + 48);
		}
		return;
			
    }

    /** Returns the new line character. */
    /** Returns the new line character. */
    function char newLine() {
		return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
		return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
		return 34;
    }
}
